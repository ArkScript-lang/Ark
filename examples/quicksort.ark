(import "List.ark")

(let tmpTailOf (fun (a) (if (<= (len a) 1) [] (tailOf a))))

(let quicksort (fun (array) {
    (if (or (empty? array) (nil? array))
        []
        {
            (let pivot (firstOf array))
            (concat
                (quicksort
                    (list:filter (tmpTailOf array) (fun (e) (< e pivot))))
                [pivot]
                (quicksort
                    (list:filter (tmpTailOf array) (fun (e) (>= e pivot)))))
        })
}))

(let a [3 6 1 5 1 65 324 765 1 6 3 0 6 9 6 5 3 2 5 6 7 64 645 7 345 432 432 4 324 23])

(let bench (fun (code) {
    (mut start (time))

    (mut i 0)
    (while (< i 25) {
        (code)
        (set i (+ 1 i))
    })

    (let t (/ (* 1000 (- (time) start)) 25))
    (print "average: " t "ms")
    t
}))

(print a)
(let ark (bench '(quicksort a)))
(let cpp (bench '(list:sort a)))
(print "ratio ark/cpp: " (/ ark cpp))