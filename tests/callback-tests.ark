{
    (import "test-tools.ark")
    
    (let callbacks-tests (fun () {
        (mut tests 0)
        (let start-time (time))

        (let egg (fun (bar) { bar }))
        (let data ["Iron Man" "is" "Tony Stark"])
        (mut callbacks [])

        (mut acc 0)
        (while (!= acc (len data)) {
            (mut d (@ data acc))
            (set callbacks (append callbacks (fun (&d) (egg d))))
            (set acc (+ 1 acc))
        })

        (set acc 0)
        (while (!= acc (len callbacks)) {
            (assert_ (= (@ data acc) (@ callbacks acc).d) "Callback test 1 failed")
            (assert_ (= (@ data acc) ((@ callbacks acc))) "Callback test 1°2 failed")
            (set acc (+ 1 acc))
        })

        (let countdown-from (fun (number)
            (fun (&number) {
                (set number (- number 1))
                number
            }))
        )
        (let countdown-from-3 (countdown-from 3))
        (assert_ (= 2 (countdown-from-3)) "Callback test 2 failed")
        (assert_ (= 1 (countdown-from-3)) "Callback test 2°2 failed")
        (assert_ (= 0 (countdown-from-3)) "Callback test 2°3 failed")

        (recap "Callback tests passed" tests (- (time) start-time))
        
        tests
    }))

    (let passed-callback (callbacks-tests))
}